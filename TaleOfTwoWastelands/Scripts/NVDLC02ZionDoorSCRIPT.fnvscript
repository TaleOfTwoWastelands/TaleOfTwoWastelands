scn NVDLC02ZionDoorSCRIPT


; Jorge S. 12/22/10
; Script to handle transition to Zion Valley.
; JSH 01/19/11 - Added auto-firing of New Vegas companions. Sorry, buddies - no Zion for you.
; OMP 02/21/2011- Added code to enable the radios when leaving Zion.


Ref rVCountryRadio;


BEGIN OnActivate

	If (IsActionRef Player == 1)
		If (Player.GetItemCount NVDLC02ZionMap == 0)
			ShowMessage NVDLC02NoCaravanMsg
		Else
			If ( NVDLC02ZionToMojaveDoorREF.bNVRadiosOff == 0 )
				RNVTARef.Disable;
				RadioBlackMountainTAREF.Disable;
				Set rVCountryRadio To NVDLC02CountryRadioMarkerREF.GetLinkedREF;
				rVCountryRadio.Disable;
				Set NVDLC02ZionToMojaveDoorREF.bNVRadiosOff To 1;
			EndIf

			; --------------------------------	NV Quests Shutdown-------------------------------	\/														; 2/9/11 ETB - Shutting down all NCR/Legion Assassin's quests (Legion "Bad2" one was causing a framerate issue).
				If ( NVDLC02MQ00.bNVQuestsDisabled == 0 )																								; Stopped here and started in script: "NVDLC02MojaveDoorSCRIPT"
					StopQuest VEFR01LegionGood2CaesarsFavor;
					StopQuest VEFR00LegionMix2CaesarsHire;
					StopQuest VEFR02LegionBad2CaesarsFoe;
	
					;StopQuest VEFR01NCRGood2EmergencyRadio;																	; [NOTE: This quest is NOT stopped (i.e. commented-out) on purpose (but included for tracking).]
					StopQuest VEFR00NCRMixed2LimitedAccess;
					StopQuest VEFR02NCRBad2MostWanted;
					Set NVDLC02MQ00.bNVQuestsDisabled to 1;
					Set NVDLC02ZionToMojaveDoorREF.bNVQuestsRestarted to 0;
				Endif
			; -------------------------------------------------------------------------------------------------	/\

			; Fire humanoid follower.
			call TTWFNFireAllFollowers

			;sets date for arrival
			Set GameDay to (GameDay + 14)
			if (GameMonth == 4) || (GameMonth == 6) || (GameMonth == 9) || (GameMonth == 11)
				if (GameDay > 30)
					set GameMonth to (GameMonth + 1)
					set GameDay to (GameDay - 30)
			     endif
			elseif (GameMonth == 12)
				if (GameDay > 31)
					set GameYear to (GameYear + 1)
					set GameMonth to 1
					set GameDay to (GameDay - 31)
			     endif
			elseif (GameMonth == 2)
			     if (GameDay > 28)
					set GameYear to (GameYear + 1)
					set GameMonth to 1
					set GameDay to (GameDay - 28)
				endif
			else
				if (GameDay > 31)
					set GameMonth to (GameMonth + 1)
					set GameDay to (GameDay - 31)
				endif
			endif


			Activate;

		EndIf

	Else

		Return;

	EndIf

END
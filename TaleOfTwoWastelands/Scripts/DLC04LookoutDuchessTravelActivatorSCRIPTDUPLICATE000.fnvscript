scn DLC04LookoutDuchessTravelActivatorSCRIPT

short PlayerTravel
short DoOnce
float Timer
short Button

BEGIN OnLoad

	if ( DoOnce == 0 )
		if ( GetStage DLC04MQ01 >= 100 )
			ForceSave
			Set DoOnce to 1
		endif
	endif

END

BEGIN OnActivate 
	
	if ( IsActionRef Player == 1)
		if player.getItemCount DLC04BoatTicket > 0
			;If the player is in the Wasteland
			if player.getInWorldspace DLC4Pointlookout == 0
				ShowMessage DLC04DuchessToPointMessage
			else ;If the player is in Point Lookout
				ShowMessage DLC04DuchessToWastelandMessage
			endif
		else
			ShowMessage DLC04DuchessNoTravel
		endif
	endif

END

BEGIN GameMode

	if ( PlayerTravel < 1 ) && ( GetButtonPressed == 1 )
		set PlayerTravel to 1
		set Timer to 5
		DisablePlayerControls 1 0 0 0 0 0
		IMod FadeToWhiteISFX
	endif

	if ( PlayerTravel == 1 )
		set Timer to Timer - GetSecondsPassed

		if ( Timer <= 0 ) 
			;Fire all followers
			call TTWFNFireAllFollowers

			;==Disable Wasteland radio stations==

			if ( RadioGalaxyNewsRef01.GetDisabled == 0 )
				RadioGalaxyNewsRef01.Disable
			endif

			if ( RadioGalaxyNewsRef02.GetDisabled == 0 )
				RadioGalaxyNewsRef02.Disable
			endif

			if ( MS16Vault101ExternalRadioREF.GetDisabled == 0 )
				MS16Vault101ExternalRadioREF.Disable
			endif

			RadioEnclaveRef.Disable

			if ( MS17RadioRef.GetDisabled == 0 )
				MS17RadioRef.Disable
			endif

			PipBoyRadioOff

			;==Move Player and Tobar==

			if ( GameMonth < 12 )
					Set GameMonth to ( GameMonth + 1 )
			elseif ( GameMonth == 12 )
				Set GameMonth to 1
			endif

			IMod FadeInFromWhiteISFX
			EnablePlayerControls

			;If this is the first trip out, play the special animation
			if GetStageDone DLC04MQ01 100 == 0
				SetStage DLC04MQ01 100
			else    ;else, move player and tobar
				player.moveto DLC04LookoutLandingMarkerREF
				DLC04TobarREF.moveto DLC04TobarWelcomeMarker
			endif
			player.removeItem DLC04BoatTicket 1
			SetStage Doctors 10
			set PlayerTravel to 0
		endif
	endif

END
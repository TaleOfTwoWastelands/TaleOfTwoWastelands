SCN DLC01TunnelHandcartDCSCRIPT

short	PlayerTravel
short	DoOnce
float	Timer

BEGIN OnLoad

	if ( DoOnce == 0 )
		if ( GetStage DLC01Quest01 >= 100 )
			ForceSave
			Set DoOnce to 1
		endif
	endif

END

BEGIN OnActivate 
	
	if ( IsActionRef Player == 1)
		if ( Player.GetDistance DLC01WernherREF <= 500 )
			if ( GetStage DLC01Quest01 == 100 ) || ( GetStage DLC01Quest01 == 91 )
				ShowMessage DLC01TravelToPittMessagebox
			else
				ShowMessage DLC01TravelToPittMessageboxNO
			endif

		elseif ( GetStageDone DLC01Quest02 200 == 0 )
			ShowMessage DLC01TravelToPittMessageboxNO
		endif

		if ( GetStageDone DLC01Quest02 200 == 1 )
			ShowMessage DLC01TravelToPittMessagebox
		endif
	endif

END

BEGIN GameMode

	if ( PlayerTravel < 1 ) && ( GetButtonPressed == 1 )
		set PlayerTravel to 1
		set Timer to 5
		DisablePlayerControls 1 0 0 0 0 0
		IMod FadeToWhiteISFX
	endif

	if ( PlayerTravel == 1 )
		set Timer to Timer - GetSecondsPassed

		if ( Timer <= 0 ) 
			;Fire all followers
			call TTWFNFireAllFollowers

			;==Disable Wasteland radio stations==

			if ( RadioGalaxyNewsRef01.GetDisabled == 0 )
				RadioGalaxyNewsRef01.Disable
			endif

			if ( RadioGalaxyNewsRef02.GetDisabled == 0 )
				RadioGalaxyNewsRef02.Disable
			endif

			if ( MS16Vault101ExternalRadioREF.GetDisabled == 0 )
				MS16Vault101ExternalRadioREF.Disable
			endif

			RadioEnclaveRef.Disable

			if ( MS17RadioRef.GetDisabled == 0 )
				MS17RadioRef.Disable
			endif

			PipBoyRadioOff

			;==Move Player and Wernher==

			;if ( GameMonth < 12 )
			;		Set GameMonth to ( GameMonth + 1 )
			;elseif ( GameMonth == 12 )
			;	Set GameMonth to 1
			;endif

			IMod FadeInFromWhiteISFX
			EnablePlayerControls
			set DLC01Quest01.WernherEscortPlayer to 1
			player.MoveTo DLC01ThePittPlayerTravelMarker
			DLC01WernherREF.MoveTo DLC01ThePittWernherTravelMarker
			SetStage Doctors 10
			set PlayerTravel to 0
		endif
	endif

END
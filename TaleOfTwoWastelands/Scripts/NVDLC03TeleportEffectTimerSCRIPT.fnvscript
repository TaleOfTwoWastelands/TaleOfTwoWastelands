scn NVDLC03TeleportEffectTimerSCRIPT


; Timer to control player's teleport fx into the Big Empty.
; JSH 10/04/10
; OMP 02/16/2011 - Moved the call to start the NVDLC03Intro quest to NVDLC03CrashedSatelliteActivatorSCRIPT.
; OMP 02/24/2011 - Trigger the MoveTo call if we're not skipping the intro.
; JSH 04/27/11 - Moved companion firing here.


Short	nStage;																	; Script blocker.
Float	fTimer;																	; Timer used for animation control.
Short bTeleportDone;															; Teleport has completed.


BEGIN GameMode

	If ( bTeleportDone == 1 )
		Return;
	EndIf

	If ( nStage == 0 )
		;Fire all followers
		call TTWFNFireAllFollowers

		NVDLC03FXTeleportREF.MoveTo Player 0 0 0;
		ApplyImageSpaceModifier NVDLC03TeleportOutISFX;
		Set fTimer To 7.5;
		Set nStage To 1;
		PlaySound OBJSateProjActivate
	EndIf

	Set fTimer To ( fTimer - GetSecondsPassed );

	If ( fTimer <= 5.5 ) && ( nStage == 1 )
		NVDLC03FXTeleportREF.Enable;									; Enable the swirling teleport effect.
		;NVDLC03FXTeleportREF.PlayGroup Forward 1;
		Set nStage To 2;
	EndIf

	If ( fTimer <= 0 ) 
		Set bTeleportDone To 1;
		If ( NVDLC03Intro.bSkipIntro == 0 )
			Player.MoveTo NVDLC03SlideShowMarkerREF;			; Teleport the player to the NVDLC03 slide show.
		EndIf
		StopQuest NVDLC03TeleportEffectTimer;
	EndIf

END
scn DLC04CapitalDuchessTravelActivatorSCRIPT

short PlayerTravel
short DoOnce
float Timer
short Button

BEGIN OnLoad

	DLC04Mirelurk1REF.kill
	DLC04Mirelurk2REF.kill
	DLC04Mirelurk3REF.kill
	DLC04Mirelurk4REF.kill
	DLC04Mirelurk5REF.kill
	DLC04Mirelurk6REF.kill

END

BEGIN OnActivate 
	
	if ( IsActionRef Player == 1)
		if player.getItemCount DLC04BoatTicket > 0 || DLC04MQ01.NadineSearch == 9

			;If the player is in the Wasteland
			if player.getInWorldspace DLC4Pointlookout == 0
				ShowMessage DLC04DuchessToPointMessage
			else ;If the player is in Point Lookout
				ShowMessage DLC04DuchessToWastelandMessage
			endif
		else
			ShowMessage DLC04DuchessNoTravel
		endif
	endif

END

BEGIN GameMode

	if ( PlayerTravel < 1 ) && ( GetButtonPressed == 1 )
		set PlayerTravel to 1
		set Timer to 5
		DisablePlayerControls 1 1 0 0 0 0
		IMod DLC04FadeToBlack
	endif

	if ( PlayerTravel == 1 )
		set Timer to Timer - GetSecondsPassed

		if ( Timer <= 0 ) 
			;Fire all followers
			call TTWFNFireAllFollowers

			;==Disable Wasteland radio stations==

			if ( RadioGalaxyNewsRef01.GetDisabled == 0 )
				RadioGalaxyNewsRef01.Disable
			endif

			if ( RadioGalaxyNewsRef02.GetDisabled == 0 )
				RadioGalaxyNewsRef02.Disable
			endif

			if ( MS16Vault101ExternalRadioREF.GetDisabled == 0 )
				MS16Vault101ExternalRadioREF.Disable
			endif

			RadioEnclaveRef.Disable

			if ( MS17RadioRef.GetDisabled == 0 )
				MS17RadioRef.Disable
			endif

			if (FFEU07RadioBeaconREF.GetDisabled == 0)
				FFEU07RadioBeaconREF.disable
				set DLC04MQ01.ChineseRadioCheck to 1
			endif

			if (MS16Vault101ExternalRadioREF.GetDisabled == 0)
				MS16Vault101ExternalRadioREF.disable
				set DLC04MQ01.MS16RadioCheck to 1
			endif

			PipBoyRadioOff

			;==Move Player and Tobar==

			if ( GameMonth < 12 )
					Set GameMonth to ( GameMonth + 1 )
			elseif ( GameMonth == 12 )
				Set GameMonth to 1
			endif
			PLSteamboatEngineDoor.enable

			EnablePlayerControls

			;If this is the first trip out, play the special animation
			if GetStageDone DLC04MQ01 100 == 0
				SetStage DLC04MQ01 100
			else    ;else, move player and tobar
				player.moveto DLC04LookoutLandingMarker02REF
				if DLC04MQ01.NadineSearch < 4
					DLC04TobarREF.moveto DLC04TobarWelcomeMarker
				else
					DLC04NadineREF.moveto DLC04TobarWelcomeMarker
				endif
				IMod DLC04FadeInFromBlackSFX
			endif
			;DLC04CBoatCont1REF.RemoveAllItems DLC04PLBoatCont1REF
			DLC04CBoatCont2REF.RemoveAllItems DLC04PLBoatCont2REF
			player.removeItem DLC04BoatTicket 1
			SetStage Doctors 10
			set PlayerTravel to 0
		endif
	endif

END